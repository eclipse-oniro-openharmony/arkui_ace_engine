# Copyright (c) 2022-2023 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("//foundation/arkui/ace_engine/ace_config.gni")
import("../../../build/uicast.gni")

template("build_component_ng") {
  forward_variables_from(invoker, "*")

  component_name = target_name
  subsystem_name = ace_engine_subsystem
  part_name = ace_engine_part

  template("build_component_ng_impl") {
    forward_variables_from(invoker, "*")

    platform = target_name
    subsystem_name = ace_engine_subsystem
    part_name = ace_engine_part
    component_deps = []
    extra_deps = []
    foreach(item, component_deps) {
      extra_deps += [ "$ace_root/frameworks/core/components/${item}:ace_core_components_${item}_${platform}" ]
    }

    component_v2_deps = []
    foreach(item, component_v2_deps) {
      extra_deps += [ "$ace_root/frameworks/core/components_v2/${item}:ace_core_components_${item}_${platform}" ]
    }

    if (defined(component_v2_deps)) {
      foreach(item, component_v2_deps) {
        extra_deps += [ "$ace_root/frameworks/core/components_ng/${item}:ace_core_components_${item}_${platform}" ]
      }
    }

    if (defined(platform_deps)) {
      foreach(item, platform_deps) {
        extra_deps += [ "${item}_${platform}" ]
      }
    }

    if (defined(deps)) {
      extra_deps += deps
    }

    ohos_source_set("ace_core_components_${component_name}_${platform}") {
      defines += invoker.defines

      sources = []
      sources += invoker.sources

      configs = []
      configs += [ "$ace_root:ace_config" ]

      if (defined(invoker.configs)) {
        configs += invoker.configs
      }

      deps = []
      deps += extra_deps
      deps += [ "$ace_root/frameworks/core/components/theme:build_theme_code" ]
      if (defined(invoker.public_configs)) {
        public_configs = []
        public_configs += invoker.public_configs
      }

      external_deps = []

      if (defined(invoker.external_deps)) {
        external_deps += invoker.external_deps
      }

      if (defined(invoker.external_deps_info)) {
        external_deps_info = []
        external_deps_info += invoker.external_deps_info
      }

      if (defined(invoker.cflags_cc)) {
        cflags_cc = []
        cflags_cc += invoker.cflags_cc
      }

      # using graphcis_2d drawing lib
      include_dirs = [
        "//base/hiviewdfx/hilog/interfaces/native/innerkits/include",
        "//foundation/graphic/graphic_2d/rosen/modules/2d_engine",
        "//foundation/graphic/graphic_2d/rosen/modules/2d_engine/rosen_text/export",
        "//foundation/graphic/graphic_2d/rosen/modules/2d_graphics/include",
        "//foundation/graphic/graphic_2d/rosen/modules/2d_graphics/src",
        "//foundation/graphic/graphic_2d/rosen/modules/2d_graphics/src/drawing/engine_adapter",
        "//third_party/bounds_checking_function/include",
      ]

      if (ace_use_new_skia) {
        configs += [ "$ace_flutter_engine_root:flutter_config" ]
        deps += [
          "$ace_flutter_engine_root/libtxt:thirdparty_lib_txt_$platform",
          "$skia_root_new:skia_$platform",
        ]
      } else {
        deps += [
          "$ace_flutter_engine_root:third_party_flutter_engine_$platform",
          "$ace_flutter_engine_root/skia:ace_skia_$platform",
        ]
      }
      if (!is_cross_platform_build && defined(config.enable_drag_framework) &&
          config.enable_drag_framework) {
        external_deps += [
          "device_status:devicestatus_client",
          "graphic_standard:librender_service_client",
        ]
        include_dirs += [
          "//base/msdp/device_status/interfaces/innerkits/interaction/include",
        ]
      }

      if (defined(config.model_component_support) &&
          config.model_component_support) {
        include_dirs += [
          "//foundation/graphic/graphic_3d/3d_widget_adapter/include",
          "//foundation/graphic/graphic_3d/3d_widget_adapter/include/custom",
          "//foundation/graphic/graphic_3d/3d_widget_adapter/include/data_type",
          "//foundation/graphic/graphic_3d/3d_widget_adapter/include/data_type/geometry",
          "//foundation/graphic/graphic_3d/3d_widget_adapter/include/ohos",
          "//foundation/graphic/graphic_2d/rosen/modules/",
        ]
      }

      if (platform == "ohos") {
        deps += [
          "//third_party/EGL:libEGL",
          "//third_party/openGLES:libGLES",
        ]
        external_deps += [
          "graphic_standard:2d_graphics",
          "graphic_standard:rosen_text",
        ]
      } else {
        deps += [ "//foundation/graphic/graphic_2d/rosen/modules/2d_graphics:2d_graphics_source_$platform" ]
      }
      deps += [ "//foundation/graphic/graphic_2d/rosen/modules/2d_engine/rosen_text:rosen_text" ]

      if (defined(config.enable_rosen_backend) && config.enable_rosen_backend &&
          defined(invoker.rosen_sources)) {
        sources += invoker.rosen_sources
        if (is_cross_platform_build) {
          deps += [ "//foundation/graphic/graphic_2d/rosen/modules/render_service_client:librender_service_client_static" ]
        } else {
          external_deps += [ "graphic_standard:librender_service_client" ]
        }

        include_dirs += [
          "//foundation/window/window_manager",
          "//foundation/window/window_manager/interfaces/innerkits",
        ]
      }

      if (defined(config.enable_standard_input) &&
          config.enable_standard_input) {
        if (defined(invoker.standard_input_sources)) {
          sources += invoker.standard_input_sources
        }
        if (defined(invoker.standard_input_deps)) {
          if (defined(external_deps)) {
            external_deps += invoker.standard_input_deps
          } else {
            external_deps = invoker.standard_input_deps
          }
        }
      }
    }
  }

  foreach(item, ace_platforms) {
    build_component_ng_impl(item.name) {
      platform = item.name
      defines = []
      defines += uicast_configs.uicast_defines

      if (defined(item.config)) {
        config = item.config
      } else {
        config = {
        }
      }

      if (defined(config.defines)) {
        defines += config.defines
      }

      if (defined(config.cflags_cc)) {
        cflags_cc = config.cflags_cc
      } else {
        cflags_cc = []
      }
    }
  }
}
