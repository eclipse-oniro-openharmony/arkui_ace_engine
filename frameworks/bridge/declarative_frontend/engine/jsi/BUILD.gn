# Copyright (c) 2021-2023 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//arkcompiler/ets_frontend/es2panda/es2abc_config.gni")
import("//build/ohos.gni")
import("//foundation/arkui/ace_engine/ace_config.gni")
import("//foundation/arkui/ace_engine/build/ace_ext.gni")
import("//foundation/arkui/ace_engine/build/ace_gen_obj.gni")

base_output_path = get_label_info(":gen_abc_proxyclass", "target_out_dir")
abcproxyclass_obj_path = base_output_path + "/abc_proxy_class.o"
abcenumstyle_obj_path = base_output_path + "/abc_enum_style.o"
abcuicontext_obj_path = base_output_path + "/abc_uicontext.o"
jsmocksystemplugin_obj_path = base_output_path + "/abc_js_mock_system_plugin.o"

es2abc_gen_abc("gen_stateMgmt_abc") {
  extra_visibility = [ ":*" ]  # Only targets in this file can depend on this.
  src_js = rebase_path(
          "//foundation/arkui/ace_engine/frameworks/bridge/declarative_frontend/engine/stateMgmt.js")
  dst_file = rebase_path(base_output_path + "/stateMgmt.abc")

  in_puts = [ "//foundation/arkui/ace_engine/frameworks/bridge/declarative_frontend/engine/stateMgmt.js" ]
  out_puts = [ base_output_path + "/stateMgmt.abc" ]
}

es2abc_gen_abc("gen_jsEnumStyle_abc") {
  extra_visibility = [ ":*" ]  # Only targets in this file can depend on this.
  src_js = rebase_path(
          "//foundation/arkui/ace_engine/frameworks/bridge/declarative_frontend/engine/jsEnumStyle.js")
  dst_file = rebase_path(base_output_path + "/jsEnumStyle.abc")

  in_puts = [ "//foundation/arkui/ace_engine/frameworks/bridge/declarative_frontend/engine/jsEnumStyle.js" ]
  out_puts = [ base_output_path + "/jsEnumStyle.abc" ]
}

es2abc_gen_abc("gen_jsUIContext_abc") {
  extra_visibility = [ ":*" ]  # Only targets in this file can depend on this.
  src_js = rebase_path(
          "//foundation/arkui/ace_engine/frameworks/bridge/declarative_frontend/engine/jsUIContext.js")
  dst_file = rebase_path(base_output_path + "/jsUIContext.abc")

  in_puts = [ "//foundation/arkui/ace_engine/frameworks/bridge/declarative_frontend/engine/jsUIContext.js" ]
  out_puts = [ base_output_path + "/jsUIContext.abc" ]
}

if (!is_cross_platform_build) {
  es2abc_gen_abc("gen_js_mock_system_plugin_abc") {
    extra_visibility = [ ":*" ]  # Only targets in this file can depend on this.
    src_js = rebase_path(
            "$root_out_dir/obj/ide/tools/previewer/automock/dist/jsMockSystemPlugin.js")
    dst_file = rebase_path(base_output_path + "/jsMockSystemPlugin.abc")

    in_puts = [ rebase_path(
            "$root_out_dir/obj/ide/tools/previewer/automock/dist/jsMockSystemPlugin.js") ]
    out_puts = [ base_output_path + "/jsMockSystemPlugin.abc" ]
    extra_dependencies =
        [ "//ide/tools/previewer/automock:gen_snapshot_jsmock" ]
  }
}

gen_obj("abc_proxyclass") {
  input = base_output_path + "/stateMgmt.abc"
  if (use_mac || use_mingw_win || use_ios || use_linux) {
    abcproxyclass_obj_path = base_output_path + "/js_proxy_class.c"
  }
  output = abcproxyclass_obj_path
  snapshot_dep = [ ":gen_stateMgmt_abc" ]
}

gen_obj("abc_enum_style") {
  input = base_output_path + "/jsEnumStyle.abc"
  if (use_mac || use_mingw_win || use_linux || use_ios) {
    abcenumstyle_obj_path = base_output_path + "/jsEnumStyle.c"
  }
  output = abcenumstyle_obj_path
  snapshot_dep = [ ":gen_jsEnumStyle_abc" ]
}

gen_obj("abc_uicontext") {
  input = base_output_path + "/jsUIContext.abc"
  if (use_mac || use_mingw_win || use_linux || use_ios) {
    abcuicontext_obj_path = base_output_path + "/jsUIContext.c"
  }
  output = abcuicontext_obj_path
  snapshot_dep = [ ":gen_jsUIContext_abc" ]
}

if (!is_cross_platform_build) {
  gen_obj("abc_mock_system_plugin") {
    input = base_output_path + "/jsMockSystemPlugin.abc"
    if (use_mac || use_mingw_win || use_linux) {
      jsmocksystemplugin_obj_path =
          base_output_path + "/js_mock_system_plugin.c"
    }
    output = jsmocksystemplugin_obj_path
    snapshot_dep = [ ":gen_js_mock_system_plugin_abc" ]
  }
}

template("declarative_js_engine_ark") {
  forward_variables_from(invoker, "*")
  platform = invoker.platform

  ohos_source_set(target_name) {
    subsystem_name = ace_engine_subsystem
    part_name = ace_engine_part
    defines += invoker.defines
    if (target_cpu == "arm64") {
      defines += [ "APP_USE_ARM64" ]
    } else if (target_cpu == "arm") {
      defines += [ "APP_USE_ARM" ]
    }

    configs = [ "$ace_root:ace_config" ]

    sources = [
      "js_converter.cpp",
      "jsi_declarative_engine.cpp",
      "jsi_declarative_group_js_bridge.cpp",
      "jsi_object_template.cpp",
      "jsi_types.cpp",
      "jsi_view_register.cpp",
      "modules/jsi_app_module.cpp",
      "modules/jsi_context_module.cpp",
      "modules/jsi_curves_module.cpp",
      "modules/jsi_matrix4_module.cpp",
      "modules/jsi_module_manager.cpp",
      "modules/jsi_router_module.cpp",
      "modules/jsi_syscap_module.cpp",
      "modules/jsi_timer_module.cpp",
    ]

    include_dirs = []
    if (defined(vendor_configs.extra_view_register_sources)) {
      include_dirs += vendor_configs.ace_advance_dirs
      sources += vendor_configs.extra_view_register_sources
    } else {
      sources += [ "jsi_extra_view_register.cpp" ]
    }

    include_dirs += [ "${ace_root}/interfaces/inner_api/ace" ]

    deps = []
    if (!is_cross_platform_build) {
      deps +=
          [ "${ace_root}/interfaces/inner_api/ace:ace_forward_compatibility" ]
    }

    if (platform == "ohos_ng" || is_cross_platform_build) {
      sources += [
        "jsi_animator_model_ng_build_impl.cpp",
        "jsi_view_register_impl_ng.cpp",
      ]
      if (is_cross_platform_build) {
        defines += [ "CROSS_PLATFORM" ]
      }
    } else {
      sources += [
        "jsi_animator_model_ng_impl.cpp",
        "jsi_view_register_impl.cpp",
      ]
      deps += [ "$ace_root/frameworks/core/components/theme:build_theme_code" ]
    }

    external_deps = []

    # add for cross_platfrom_build temporarily, shall be removed after external_deps is supported
    if (is_cross_platform_build) {
      configs += [ "$ark_ets_path:ark_jsruntime_public_config" ]
      deps += [ "$ark_ets_path:libark_jsruntime_static" ]
      public_deps = [ "//foundation/arkui/napi:ace_napi" ]
    } else {
      external_deps += [
        "ets_runtime:libark_jsruntime",
        "napi:ace_napi",
      ]
    }

    if (defined(config.build_for_preview) && config.build_for_preview) {
      deps += [ ":gen_obj_src_abc_mock_system_plugin" ]
      sources += [ "utils/jsi_module_searcher.cpp" ]
    } else if (defined(config.use_build_in_js_engine) &&
               config.use_build_in_js_engine) {
      if (platform == "ohos_ng" || is_cross_platform_build) {
        sources += [
          "$ace_root/frameworks/bridge/js_frontend/engine/common/js_api_perf.cpp",
          "$ace_root/frameworks/bridge/js_frontend/engine/jsi/ark_js_runtime.cpp",
          "$ace_root/frameworks/bridge/js_frontend/engine/jsi/ark_js_value.cpp",
          "$ace_root/frameworks/bridge/js_frontend/engine/jsi/jsi_base_utils.cpp",
        ]
      }
    } else {
      sources += [
        "$ace_root/frameworks/bridge/js_frontend/engine/jsi/ark_js_runtime.cpp",
        "$ace_root/frameworks/bridge/js_frontend/engine/jsi/ark_js_value.cpp",
        "$ace_root/frameworks/bridge/js_frontend/engine/jsi/jsi_base_utils.cpp",
      ]
    }

    deps += [
      ":gen_obj_src_abc_enum_style",
      ":gen_obj_src_abc_proxyclass",
      ":gen_obj_src_abc_uicontext",
    ]
  }
}

foreach(item, ace_platforms) {
  platform = item.name
  engine_config = {
  }
  engine_config = item.config
  support_engines = []
  support_engines = engine_config.js_engines
  foreach(engine, support_engines) {
    if (engine.engine_name == "ark") {
      declarative_js_engine_ark("declarative_js_engine_bridge_ark_$platform") {
        defines = engine.engine_defines
        config = {
        }

        if (defined(item.config)) {
          config = item.config
        }
        if (defined(config.defines)) {
          defines += config.defines
        }
      }
    }
  }
}
